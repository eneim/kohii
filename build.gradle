/*
 * Copyright (c) 2018 Nam Nguyen, nam@ene.im
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.
import kohii.Libs

buildscript {
  repositories {
    google()
    mavenCentral()
    gradlePluginPortal()
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://plugins.gradle.org/m2/" }
  }

  dependencies {
    //noinspection GradleDependency
    classpath Libs.Common.androidGradlePlugin
    classpath Libs.Kotlin.gradlePlugin
    // classpath Libs.Common.dexcountGradlePlugin
    classpath Libs.Google.gmsGoogleServices
    classpath Libs.Google.firebaseCrashlyticsPlugin
    classpath Libs.Common.ktLintPlugin
    // classpath Libs.Common.bintrayPlugin
    classpath Libs.Common.dokkaPlugin
    classpath Libs.Common.binaryValidator
    // classpath Libs.Common.mavenPublish
    classpath Libs.Common.nexusPublish
  }
}

apply plugin: 'io.github.gradle-nexus.publish-plugin'

// Setup for the Nexus Publish Plugin.
group = GROUP
version = VERSION_NAME

def sampleProjects = [project("kohii-sample"), project("kohii-sample-tiktok")]
file("$rootDir/kohii-samples").eachDir { dir ->
  if (file("$dir/build.gradle").exists() || file("$dir/build.gradle.kts").exists()) {
    sampleProjects += project(":kohii-samples:" + dir.name)
  }
}

apply plugin: 'binary-compatibility-validator'
apiValidation {
  sampleProjects.forEach {
    ignoredProjects += it.name
  }
}

apply plugin: "org.jetbrains.dokka"
tasks.withType(org.jetbrains.dokka.gradle.DokkaMultiModuleTask).configureEach {
  outputDirectory.set(file("$rootDir/docs/api"))
  removeChildTasks(sampleProjects)
}

def getRepositoryUsername() {
  return hasProperty('mavenCentralRepositoryUsername') ? mavenCentralRepositoryUsername : ""
}

def getRepositoryPassword() {
  return hasProperty('mavenCentralRepositoryPassword') ? mavenCentralRepositoryPassword : ""
}

nexusPublishing {
  repositories {
    sonatype {
      username = getRepositoryUsername()
      password = getRepositoryPassword()
    }
  }
}

allprojects {
  repositories {
    google()
    mavenCentral()
    //noinspection JcenterRepositoryObsolete,GrDeprecatedAPIUsage
    jcenter() // For ExoPlayer 2.11.x
    maven { url "https://jitpack.io" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    /* flatDir {
      dirs 'libs'
    } */
  }

  apply plugin: "org.jlleitschuh.gradle.ktlint" // Version should be inherited from parent
  apply plugin: "org.jetbrains.dokka"

  tasks.withType(JavaCompile).configureEach { task ->
    task.sourceCompatibility = JavaVersion.VERSION_1_8
    task.targetCompatibility = JavaVersion.VERSION_1_8
  }

  //noinspection UnnecessaryQualifiedReference
  tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompile).configureEach { task ->
    task.kotlinOptions {
      jvmTarget = "1.8"
    }
  }

  //noinspection UnnecessaryQualifiedReference
  tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinCompile).configureEach { task ->
    task.kotlinOptions {
      freeCompilerArgs += [
          '-XXLanguage:+InlineClasses',
          '-Xjvm-default=enable'
      ]
    }
  }

  tasks.withType(Test) {
    testLogging {
      events "skipped", "failed", "passed"
    }
  }

  tasks.withType(org.jetbrains.dokka.gradle.DokkaTaskPartial).configureEach {
    dokkaSourceSets.configureEach {
      jdkVersion.set(8)
      skipDeprecated.set(true)

      externalDocumentationLink {
        url.set(URL("https://developer.android.com/reference/"))
      }
      externalDocumentationLink {
        url.set(URL("https://exoplayer.dev/doc/reference/"))
      }
    }
  }

  afterEvaluate { p ->
    if (p.hasProperty("android")) {
      p.android.lintOptions {
        // FIXME(eneim): temporarily disable.
        disable "RestrictedApi", "VisibleForTests", "UnusedResources", "UnusedIds"
      }
    }

    if (p.hasProperty("android") && p.hasProperty("dependencies")) {
      p.dependencies {
        dokkaHtmlPartialPlugin(Libs.Common.dokkaAndroidPlugin)
      }
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

tasks.wrapper {
  distributionType = Wrapper.DistributionType.ALL
}

// copy from https://github.com/chrisbanes/tivi
// read from gradle.properties for a value, or use default instead.
String lookUpProf(String propertyName, String defaultValue) {
  def propertyValue = project.properties[propertyName]
  return propertyValue != null ? propertyValue : defaultValue
}
