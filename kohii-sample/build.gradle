/*
 * Copyright (c) 2018 Nam Nguyen, nam@ene.im
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.getkeepsafe.dexcount'

androidExtensions {
  experimental = true
}

if (file("google-services.json").exists()) {
  System.out.println("has google-services.json")
  apply plugin: 'com.google.gms.google-services'
  apply plugin: 'io.fabric'
} else {
  System.out.println("google-services.json not found.")
}

android {
  compileSdkVersion buildConfig.compileSdk

  signingConfigs {
    release {
      keyAlias kohii_prodKeyAlias
      keyPassword prodKeyPassword
      storeFile file(prodStoreFile)
      storePassword prodStorePassword
    }

    debug {
      keyAlias "debug"
      keyPassword "android"
      storeFile file("debug.jks")
      storePassword "android"
    }
  }

  defaultConfig {
    applicationId "kohii.v1.sample"
    minSdkVersion 21 /* buildConfig.minSdk */
    targetSdkVersion buildConfig.targetSdk
    versionCode rootProject.gitCommitCount /* buildConfig.version.code */
    versionName buildConfig.version.name

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    testApplicationId 'kohii.v1.sample.test'
  }

  buildTypes {

    release {
      signingConfig signingConfigs.release
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }

    debug {
      applicationIdSuffix ".dev"
      signingConfig signingConfigs.debug
      // multiDexEnabled true
      minifyEnabled false
      shrinkResources false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
          'proguard-rules-debug.pro'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  dataBinding.enabled = true
  productFlavors {}
}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  implementation project(':kohii')
  implementation libraries.exoplayer.all

  implementation libraries.kotlin.stdlib
  implementation libraries.androidx.appcompat
  implementation libraries.androidx.recyclerview
  implementation libraries.androidx.rvSelection
  implementation libraries.androidx.material
  implementation libraries.androidx.constraintlayout
  implementation libraries.androidx.lifecycle
  implementation libraries.androidx.core

  testImplementation libraries.junit
  androidTestImplementation libraries.xtest.runner
  androidTestImplementation libraries.xtest.espresso.core

  implementation 'com.squareup.moshi:moshi:1.8.0'
  implementation("com.squareup.moshi:moshi-kotlin:1.8.0") {
    exclude group: "org.jetbrains.kotlin", module: "kotlin-reflect"
  }
  implementation "org.jetbrains.kotlin:kotlin-reflect:1.3.20"
  implementation "com.squareup.okio:okio:2.2.2"

  implementation "com.github.bumptech.glide:glide:4.9.0-SNAPSHOT"
  kapt "com.github.bumptech.glide:compiler:4.9.0-SNAPSHOT"
  implementation 'com.caverock:androidsvg:1.2.1'

  debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.3'
  releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'
  // Optional, if you use support library fragments:
  debugImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.3'

  implementation "com.crashlytics.sdk.android:crashlytics:2.9.8"
  implementation 'com.google.firebase:firebase-core:16.0.7'
}
